/**
 * io.ul
 * Copyright (C) Paul Passeron
 * Unilang Standard Library Input/Output 
 * Paul Passeron <paul.passeron2@gmail.com>
**/

@include "syscalls.ul"
@include "string.ul"
@include "stat.ul"

/**
 * putchar - Prints a char on the standard output
 * @param c: The char to print
 * @return: void
**/
let putchar(c: char): void => {
  syswrite(1, string_to_cstr(char_to_string(c)), 1);
}

/**
 * print - Prints a string on the standard output
 * @param s: The string to print
 * @return: void
**/
let print(s: string): void => {
  syswrite(1, s.contents, s.length);
}

/**
 * print_num - Prints a number on the standard output
 * @param n: The numnber to print
 * @return: void
**/
let print_num(n: i32): void => {
  if (n < 0) => {
    putchar('-');
    n => -n;
  }
  let a: i32 => n / 10;
  if (a != 0) =>  {
    print_num(a);
  }
  putchar(n % 10 + '0');
}

/**
 * println - Prints a string with a new line at the end
 * @param s: the string to print
 * @return: void
**/
let println(s: string): void => {
  print(s);
  putchar(10);
}


let o_rdonly: i32 => 0;
let o_wronly: i32 => 1;
let o_creat: i32 => 64;



/**
 * File struct, used as a handle when dealing with files
**/
struct file => {
  fd: u64,
  mode: i8,
  omode: string,
  filename: string,
  is_valid: bool,
  stats: stat,



  /**
  * open - Opens file
  * @param path: the path to the file
  * @param flag: the flags to open the file, this defines the behavior of the returned handle
  * @return: void
  **/
  let open(path: string, omode: string): void => {
    this.is_valid => 1;
    this.filename => path;
    this.omode => omode;
    let mode: i32 => 0666;
    if omode == "w" => {
      this.fd => syscreate(path, mode);
      if this.fd >= 0 => {
        let flags: i32 => o_wronly | o_creat;
        this.fd => sysopen(path, flags, mode);
        if this.fd < 0 => {
          this.is_valid => 0;
          println("Error opening file \'" + path + "\'");
        }
      }
      else {
        this.is_valid => 0;
        println("Error creating file \'" + path + "\'");
      }
    } else if omode == "r" => {
       let flags: i32 => o_rdonly;
        this.fd => sysopen(path, flags, mode);
        if this.fd < 0 => {
          this.is_valid => 0;
          println("Error opening file \'" + path + "\'");
        }
    } else {
      this.is_valid => 0;
      println("Invalide file opening mode " + omode);
    } 
  },

  /**
  * close - Closes file
  * @return: void
  **/
  let close() : void => {
    if this.is_valid =>
      sysclose(this.fd);
    this.is_valid => 0;
  },


  /**
  * print - Prints a string to the file
  * @param s: the string to print
  * @return: void
  **/
  let print(s: string): void => {
    if this.is_valid =>
      syswrite(this.fd, s.contents, s.length);
  },

   /**
  * println - Prints a string followed by a new line to the file
  * @param s: the string to print
  * @return: void
  **/
  let println(s: string): void => {
   this.print(s + "\n");
  },

  let getstat(): stat => {
    let s: stat;
    sysstat(this.filename, s);
    return s;
  },

  let size(): i32 => {
    if !this.is_valid => return -1;
    this.stats => this.getstat();
    return this.stats.stat_size;
  },

  let read(): string => {
    if !this.is_valid => {
      println("Could not read: Invalid file");
      return "";
    }
    if this.omode != "r" => {
      println("Could not read: Invalid mode: " + this.omode);
      return "";
    }
    let size: i32 => this.size();
    let s: string => sysread(this.fd, size);
    s.length => size;
    return s;
  },

}





